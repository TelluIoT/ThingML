module org.thingml.xtext.GenerateThingML

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "thingml"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				web = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "windows-1252"
				fileHeader = "/**
 * Licensed under the Apache License, Version 2.0 (the \"License\");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an \"AS IS\" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * See the NOTICE file distributed with this work for additional
 * information regarding copyright ownership.
 */"
			}
		}
		language = StandardLanguage {
			name = "org.thingml.xtext.ThingML"
			fileExtensions = "thingml"

			serializer = {
				generateStub = true
				
			}
			formatter = {
				generateStub = true
			}
			validator = {
				generateStub = true
				composedCheck = "org.thingml.xtext.validation.checks.FunctionUsage"
				composedCheck = "org.thingml.xtext.validation.checks.VariableUsage"
				composedCheck = "org.thingml.xtext.validation.checks.ThingsUsage"
				composedCheck = "org.thingml.xtext.validation.checks.StateUsage"
				composedCheck = "org.thingml.xtext.validation.checks.TransitionUsage"
				composedCheck = "org.thingml.xtext.validation.checks.MessageUsage"
				composedCheck = "org.thingml.xtext.validation.checks.AutotransitionCycles"
				composedCheck = "org.thingml.xtext.validation.checks.PortsUsage"
				composedCheck = "org.thingml.xtext.validation.checks.ControlStructures"
				composedCheck = "org.thingml.xtext.validation.checks.LostMessages"
				composedCheck = "org.thingml.xtext.validation.checks.PropertyInitialization"
				composedCheck = "org.thingml.xtext.validation.checks.Imports"
				composedCheck = "org.thingml.xtext.validation.checks.Datatypes"
				composedCheck = "org.thingml.xtext.validation.checks.Annotations"
			}
			
			quickFixProvider = {
				generateStub = true
			}
			
		}
	}
}
