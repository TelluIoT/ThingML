import "../core/arduino.thingml"
import "../devices/analog_input.thingml"

thing fragment TemperatureSensorMsgs includes AnalogInputMsgs 
{
	message read_temperature ();
	message temperature_value (val : Int16);
}

thing TemperatureSensor includes TemperatureSensorMsgs
@c_header 
"
#include <math.h>
int temperature (int RawADC) {
 double Temp;
 Temp = log(((10240000/RawADC) - 10000));
 Temp = 1 / (0.001129148 + (0.000234125 + (0.0000000876741 * Temp * Temp ))* Temp );
 Temp = Temp - 273.15;            // Convert Kelvin to Celcius
 return (int)Temp;
}
"
{   
	provided port TemperatureSensor 
    {
		receives read_temperature
		sends temperature_value
	}

    required port AnalogInput
    {
        receives analog_input_value
        sends read_analog_input    
    }
    
    statechart TempSensorImpl init Running 
    {    
    	internal event m : AnalogInput?analog_input_value
    	action TemperatureSensor!temperature_value ('temperature ('& m.value &')')
    	
        state Running 
        {
        	internal event TemperatureSensor?read_temperature
        	action AnalogInput!read_analog_input ()
        }        
    }
}


configuration fragment TemperatureSensor
{
    group io : OnChangeAnalogInput

    instance temperature_sensor : TemperatureSensor
    connector temperature_sensor.AnalogInput => io.analog_input.AnalogInput
} 