import "../core/arduino.thingml"
import "../core/digital_output.thingml"
import "../core/arduino_timer.thingml"

thing fragment BuzzerMsgs
{
	message buzzer_on ();
	message buzzer_off ();
	message buzzer_delay (delay : UInt16);
}

thing Buzzer includes BuzzerMsgs, DigitalOutputMsgs, TimerMsgs
{ 
	provided port Buzzer 
    {
		receives buzzer_on, buzzer_off, buzzer_delay
	}

    required port DigitalOutput
    {
        sends set_digital_output    
    }
    
    required port Timer
    {
    	sends timer_start, timer_cancel
    	receives timer_timeout
    }
    
    statechart BuzzerImpl init BuzzerOff 
    {    
        state BuzzerOff 
        {
            on entry DigitalOutput!set_digital_output (DigitalState:LOW)
            
            transition switch_on -> BuzzerOn 
            event Buzzer?buzzer_on 
            
            transition delay -> BuzzerOn
            event m : Buzzer?buzzer_delay
            action Timer!timer_start (m.delay)
        }
        
        state BuzzerOn
        {
            on entry DigitalOutput!set_digital_output (DigitalState:HIGH)
  
            transition switch_off -> BuzzerOff 
            event Buzzer?buzzer_off 
            event Timer?timer_timeout
            action Timer!timer_cancel ()   
        }
    }
}

configuration fragment Buzzer
{
    group io : DigitalOutput   
    
    instance timer : SoftTimer
    instance buzzer : Buzzer	 
    connector buzzer.DigitalOutput => io.digital_output.DigitalOutput
    connector buzzer.Timer => timer.timer
} 