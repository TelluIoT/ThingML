import "../core/arduino.thingml"
import "../core/analog_input.thingml"

thing fragment SoundSensorMsgs 
{
	message read_sound ();
	message sound_value (val : UInt16);
}

thing SoundSensor includes SoundSensorMsgs, AnalogInputMsgs
{
    readonly property sample_rate : UInt8 = 100
    property counter : UInt8 = 0
    property max_value : UInt16 = 0
    
	provided port SoundSensor 
    {
		receives read_sound
		sends sound_value
	}

    required port AnalogInput
    {
        receives analog_input_value
        sends read_analog_input    
    }
    
    statechart SoundSensorImpl init Running 
    {    
        internal event m : AnalogInput?analog_input_value
        guard counter < sample_rate
    	action do
            if (m.value > max_value) max_value = m.value
            counter = counter + 1
            end 
        	
        state Running 
        {
            internal event m : AnalogInput?analog_input_value
            guard counter == sample_rate or counter > sample_rate
    	    action do
                SoundSensor!sound_value (max_value)
                counter = 0
                max_value = 0
            end
            
        	transition force_reading -> Read 
            event SoundSensor?read_sound
        	action AnalogInput!read_analog_input ()
        }
        
        state Read
        {
            transition read -> Running 
            event m : AnalogInput?analog_input_value
    	    action SoundSensor!sound_value (max_value)
        }     
    }
}


configuration fragment SoundSensor
{
    group io : OnChangeAnalogInput
        set io.analog_input.refresh_rate = 1

    instance sound_sensor : SoundSensor
    connector sound_sensor.AnalogInput => io.analog_input.AnalogInput
} 