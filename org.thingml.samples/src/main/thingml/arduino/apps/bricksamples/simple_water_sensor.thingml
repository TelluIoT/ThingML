import "../../core/arduino.thingml"
import "../../bricks/led.thingml"
import "../../bricks/water_sensor.thingml"

//*
// This is an example of ThingML use to generate Arduino code which uses 
// Electronic Brick components.
//  
// Requirements:
//      - water sensor (1)      ->  Default pin: D_9 
//      - led (1)               ->  Default pin: D_8
//
// The led is on if the sensor detects water.
//*

thing SimpleWaterSensor includes LedMsgs, WaterSensorMsgs
{
	required port Led
	{
		sends led_on, led_off
	}
	
	required port WaterSensor
	{
		receives water_value
	}
		
	statechart SimpleWaterSensorImpl init Idle
	{
		state Idle
		{
			transition dip -> LedOn
			event m : WaterSensor?water_value
			guard m.val == DigitalState:LOW
			action Led!led_on ()
		}
		
		state LedOn
		{
			transition dry -> Idle
			event m : WaterSensor?water_value
			guard m.val == DigitalState:HIGH
			action Led!led_off ()
		}
	}
}

configuration SimpleWaterSensor
{ 
    group water_sensor : WaterSensor
        set water_sensor.io.digital_input.pin = DigitalPin:PIN_9
    group led : Led
        set led.io.digital_output.pin = DigitalPin:PIN_8
    
    instance simple_water_sensor : SimpleWaterSensor
	connector simple_water_sensor.Led => led.led.Led
	connector simple_water_sensor.WaterSensor => water_sensor.water_sensor.WaterSensor
}