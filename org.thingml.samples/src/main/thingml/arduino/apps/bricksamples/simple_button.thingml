import "../../core/arduino.thingml"
import "../../bricks/led.thingml"
import "../../bricks/button.thingml"

//*
// This is an example of ThingML use to generate Arduino code which uses 
// Electronic Brick components.
//  
// Requirements:
//      - button (1)    ->  Default pin: D_9 
//      - led (1)       ->  Default pin: D_8
//
// The led is on while the button is pressed and off when it is released.
//*

thing SimpleButton includes LedMsgs, ButtonMsgs
{
	required port Led
	{
		sends led_on, led_off
	}
	
	required port Button
	{
		receives press, release
	}
		
	statechart SimpleButtonImpl init Idle
	{
		state Idle
		{
			transition pressed -> LedOn
			event Button?press
			action Led!led_on ()
		}
		
		state LedOn
		{
			transition released -> Idle
			event Button?release
			action Led!led_off ()
		}
	}
}

configuration SimpleButton
{ 
    group button : Button
        set button.io.digital_input.pin = DigitalPin:PIN_9
    group led : Led
        set led.io.digital_output.pin = DigitalPin:PIN_8
    
    instance simple_button : SimpleButton
	connector simple_button.Led => led.led.Led
	connector simple_button.Button => button.button.Button
}