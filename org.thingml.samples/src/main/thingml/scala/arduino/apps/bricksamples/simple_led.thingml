import "../../bricks/led.thingml"
import "../../bricks/button.thingml"
import "../../devices/softtimer.thingml"

thing SimpleLed includes LedMsgs, ButtonMsgs, TimerMsgs
{
	readonly property frequency : UInt16 = 1000
	
    required port Button
    {
        receives pressed, released    
    }

	required port Led
	{
		sends led_toggle
	}
	
	required port Timer
	{
		sends timer_start, timer_cancel
		receives timer_timeout
	}
    
    required port Led2
	{
		sends led_toggle
	}
    
	required port Timer2
	{
		sends timer_start, timer_cancel
		receives timer_timeout
	}
	
	statechart SimpleLedImpl init Idle
	{
		state Idle
        {
           transition -> Running
           event Button?released
        }

        state Running
		{
			on entry Timer!timer_start(frequency)
			
			transition toggle -> Running
			event Timer?timer_timeout
			action Led!led_toggle()

            transition -> Idle
            event Button?released
            action Timer!timer_cancel()
		}

        region test init Idle {
		    state Idle
            {
                transition -> Running
                event Button?pressed
            }

            state Running
		    {
			     on entry Timer2!timer_start(frequency)
			
			     transition toggle -> Running
			     event Timer2?timer_timeout
			     action Led2!led_toggle()

                 transition -> Idle
                 event Button?pressed
                 action Timer2!timer_cancel()
		     }
            
        }
	}
}

configuration fragment ScalaSimpleLedSimulatorFragment
{		
	// Led configuration.
	instance led : Led
    instance led2 : Led
		
	// Timer configuration.
	instance timer : SoftTimer
    instance timer2 : SoftTimer

    //Button configuration
    instance button : Button
    connector button.Sensor => button.SensorMockUp
	
	// Simple led configuration.
	instance simple_led : SimpleLed
        set frequency = 800
	connector simple_led.Led => led.Led
	connector simple_led.Timer => timer.timer
	connector simple_led.Led2 => led2.Led
	connector simple_led.Timer2 => timer2.timer
    connector simple_led.Button => button.Button
}

configuration ScalaSimpleLedSimulator
{
    group g1 : ScalaSimpleLedSimulatorFragment
    group g2 : ScalaSimpleLedSimulatorFragment
    group g3 : ScalaSimpleLedSimulatorFragment
    group g4 : ScalaSimpleLedSimulatorFragment
    group g5 : ScalaSimpleLedSimulatorFragment
    group g6 : ScalaSimpleLedSimulatorFragment
    group g7 : ScalaSimpleLedSimulatorFragment
    group g8 : ScalaSimpleLedSimulatorFragment
    group g9 : ScalaSimpleLedSimulatorFragment
    group g10 : ScalaSimpleLedSimulatorFragment
}