import "./API.thingml"

thing fragment FSC_Interface includes bCMSMsgs {
    
    required port out {
    	sends initCom, crisisDetails, numberOfTrucks, ackRoute, dispatched, arrived, done, close
    }    
    
    provided port in {
        receives initCom, crisisDetails, numberOfTrucks, route, dispatched, arrived, done, close
    }
    
}

thing FSC_Mock includes FSC_Interface
@mock "true"
{}


datatype FSC_GUI
@java_type "org.thingml.generated.gui.FSC_MockMock";

thing FSC_GUI includes GUIbCMSMsgs, bCMSMsgs 
@java_interface "org.thingml.generated.gui.FSC_MockListener" 
{

	readonly property gui : FSC_GUI = 'new org.thingml.generated.gui.FSC_MockMock("FSC")'

    provided port control {
        receives gui_initCom, gui_crisisDetails, gui_numberOfTrucks, gui_ackRoute, gui_dispatched, gui_arrived, gui_done, gui_close
    }
    
    required port out {
    	sends initCom, crisisDetails, numberOfTrucks, ackRoute, dispatched, arrived, done, close
    }    
    
      
    /**
    * Call backs for the GUI
    */
    function onInitCom_via_out(id : String) @override "true" @implements "true"
    do 
        out!initCom(id)
    end
    
    function onCrisisDetails_via_out(id : String, lat : Double, lon : Double, time : Long, isActive : Boolean, desc : String) @override "true" @implements "true"
    do 
        out!crisisDetails(id, lat, lon, time, isActive, desc)
    end
    
    function onNumberOfTrucks_via_out(number : Long) @override "true" @implements "true"
    do 
        out!numberOfTrucks(number)
    end
    
    function onAckRoute_via_out() @override "true" @implements "true"
    do 
        out!ackRoute()
    end
    
    function onDispatched_via_out() @override "true" @implements "true"
    do 
        out!dispatched()
    end
    
    function onArrived_via_out() @override "true" @implements "true"
    do 
        out!arrived()
    end
    
    function onDone_via_out() @override "true" @implements "true"
    do 
        out!done()
    end
    
    function onClose_via_out() @implements "true"
    do 
        out!close()
    end

	statechart behavior init Init {
	
		on entry do
            '' & gui & '.listeners.add(FSC_GUI.this);'
            '' & gui & '.disableAll();'
            print("FSC GUI initialized")
		end
	
		state Init {

        internal event c : control?gui_initCom
        action do
            print("gui_initCom received")
            '' & gui & '.sendinitCom_via_out.setEnabled(' & c.activate & ');'
        end
        
        internal event c : control?gui_crisisDetails
        action '' & gui & '.sendcrisisDetails_via_out.setEnabled(' & c.activate & ');'

        internal event c : control?gui_numberOfTrucks
        action '' & gui & '.sendnumberOfTrucks_via_out.setEnabled(' & c.activate & ');'

        internal event c : control?gui_ackRoute
        action '' & gui & '.sendackRoute_via_out.setEnabled(' & c.activate & ');'
        
        internal event c : control?gui_dispatched
        action '' & gui & '.senddispatched_via_out.setEnabled(' & c.activate & ');'
        
        internal event c : control?gui_arrived
        action '' & gui & '.sendarrived_via_out.setEnabled(' & c.activate & ');'

        internal event c : control?gui_done
        action '' & gui & '.senddone_via_out.setEnabled(' & c.activate & ');'
        
        internal event c : control?gui_close
        action '' & gui & '.sendclose_via_out.setEnabled(' & c.activate & ');'
      }
	}

         
}

thing FSC includes FSC_Interface, GUIbCMS_Controller {

    required port control {
        sends gui_initCom, gui_crisisDetails, gui_numberOfTrucks, gui_ackRoute, gui_dispatched, gui_arrived, gui_done, gui_close
    }
    
    provided port feedback {
    	receives initCom, crisisDetails, numberOfTrucks, ackRoute, dispatched, arrived, done, close
    }   
    
    property pscID : String
    property numberFireTrucks : Long
    property numberPolicsTrucks : Long
    
    	statechart behavior init Wait4Com {
	
		on entry do
            print("behavior.onEntry")
            control!gui_initCom(true)
            control!gui_crisisDetails(false)
            control!gui_numberOfTrucks(false)
            control!gui_ackRoute(false)
            control!gui_dispatched(false)
            control!gui_arrived(false)
            control!gui_done(false)
            control!gui_close(false)
		end
	
        //1.
		state Wait4Com {
			on entry do
                print("Wait4Com.onEntry")
				control!gui_initCom(true)
			end
			
			on exit do
				control!gui_initCom(false)
			end
			
			transition -> Wait4CrisisDetails
			event internIN?initComOK
		}
		
        //2.
		state Wait4CrisisDetails {
			on entry do
				control!gui_crisisDetails(true)
			end

            on exit do
				control!gui_crisisDetails(false)
			end

            transition -> Wait4NumberOfTrucks
			event internIN?crisisDetailsOK
	   }

        //3.1
        state Wait4NumberOfTrucks {
        	on entry do
				control!gui_numberOfTrucks(true)
			end

            on exit do
				control!gui_numberOfTrucks(false)
			end    

            transition -> WaitRoute
			event internIN?numberOfTrucksOK
        }
        
        //3.2
        state WaitRoute {
        	on entry do
				print("Waiting for route...")
			end

            on exit do
				print("Route received!")
			end                
            
            transition -> AckRoute
			event in?route
        }
            
        //3.3
        state AckRoute {
            on entry do
				control!gui_ackRoute(true)
			end

            on exit do
				control!gui_ackRoute(false)
			end     
            
            transition -> Wait4Dispatch
			event feedback?ackRoute
            action do
                out!ackRoute()
            end    
        }
        
        //4.
        state Wait4Dispatch {
            on entry do
				control!gui_dispatched(true)
			end

            on exit do
				control!gui_dispatched(false)
			end  
            
            transition -> Wait4Arrival
            event internIN?dispatchedOK

        }
        
        state Wait4Arrival {
            on entry do
				control!gui_arrived(true)
			end

            on exit do
				control!gui_arrived(false)
			end  
            
            transition -> Wait4Completion
            event internIN?arrivedOK

        }
        
        state Wait4Completion {
            on entry do
				control!gui_done(true)
			end

            on exit do
				control!gui_done(false)
			end  
            
            transition -> Wait4Close
            event internIN?doneOK

        }
        
        state Wait4Close {
            on entry do
				control!gui_close(true)
			end

            on exit do
				control!gui_close(false)
			end  
            
            transition -> Wait4Com
            event internIN?closeOK            
        }
        
        /**
         *  This regions does some "Complex" Event Processing
         *  i.e., relate several messages to produce higher level messages.
         *  This way, we can keep the main state machine free from any noise.
         */
        region CEP init Wait4Com {    
            
            //1.
            state Wait4Com{
                property gui : Boolean
                property fire : Boolean
                
                on entry
                do
                 gui = false
                 fire = false
                end
                
                internal event ic : feedback?initCom
                action do 
                    gui = true
                    out!initCom(ic.id)
                end
                
                internal event com : in?initCom
                action do 
                    fire = true
                    pscID = com.id
                end
                
                transition -> Wait4CrisisDetails
                guard (gui and fire)
                action do
                    internOUT!initComOK()
                end
                
            }
            
            //2
            state Wait4CrisisDetails {
                property gui : Boolean
                property fire : Boolean
                
                on entry
                do
                 gui = false
                 fire = false
                end
                
                internal event cd : feedback?crisisDetails
                action do 
                    gui = true
                    out!crisisDetails(cd.id, cd.lat, cd.lon, cd.time, cd.isActive, cd.desc)
                end
                
                internal event in?crisisDetails
                action do 
                    fire = true
                end
                
                transition -> Wait4NumberOfTrucks
                guard (gui and fire)
                action do
                    internOUT!crisisDetailsOK()
                end
                
            }
            
            //3.1
            state Wait4NumberOfTrucks {
                property gui : Boolean
                property fire : Boolean
                
                on entry
                do
                 gui = false
                 fire = false
                end
                
                internal event nt : feedback?numberOfTrucks
                action do 
                    gui = true
                    out!numberOfTrucks(nt.number)
                end
                
                internal event in?numberOfTrucks
                action do 
                    fire = true
                end
                
                transition -> Wait4Dispatch
                guard (gui and fire)
                action do 
                    internOUT!numberOfTrucksOK()
                end
                
            }
            
            //4.
            state Wait4Dispatch {
                property gui : Boolean
                property fire : Boolean
                
                on entry
                do
                 gui = false
                 fire = false
                end
                
                internal event feedback?dispatched
                action do 
                    gui = true
                    out!dispatched()
                end
                
                internal event in?dispatched
                action do 
                    fire = true
                end
                
                transition -> Wait4Arrival
                guard (gui and fire)
                action do 
                    internOUT!dispatchedOK()
                end
                
            }
            
            
            //5.
            state Wait4Arrival {
                property gui : Boolean
                property fire : Boolean
                
                on entry
                do
                 gui = false
                 fire = false
                end
                
                internal event feedback?arrived
                action do 
                    gui = true
                    out!arrived()
                end
                
                internal event in?arrived
                action do 
                    fire = true
                end
                
                transition -> Wait4Completion
                guard (gui and fire)
                action do 
                    internOUT!arrivedOK()
                end
                
            }
            
            
            //6.
            state Wait4Completion {
                property gui : Boolean
                property fire : Boolean
                
                on entry
                do
                 gui = false
                 fire = false
                end
                
                internal event feedback?done
                action do 
                    gui = true
                    out!done()
                end
                
                internal event in?done
                action do 
                    fire = true
                end
                
                transition -> Wait4Close
                guard (gui and fire)
                action do 
                    internOUT!doneOK()
                end
                
            }
            
            
            //7.
            state Wait4Close {
                property gui : Boolean
                property fire : Boolean
                
                on entry
                do
                 gui = false
                 fire = false
                end
                
                internal event feedback?close
                action do 
                    gui = true
                    out!close()
                end
                
                internal event in?close
                action do 
                    fire = true
                end
                
                transition -> Wait4Com
                guard (gui and fire)
                action do 
                    internOUT!closeOK()
                end
                
            }
                        
            
        }
                        
	
	}

}

/*configuration testFSC {
    instance fsc_gui : FSC_GUI    
    instance fsc : FSC  
    instance bcms : bCMS_Mock
    
    connector fsc_gui.out => fsc.feedback
    connector fsc.control => fsc_gui.control
    connector fsc.out => bcms.fromFSC
    connector fsc.internOUT => fsc.internIN
    connector bcms.toFSC => fsc.in
}*/