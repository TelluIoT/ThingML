import "../timer.thingml"

datatype JThread
@java_type "Thread";

// Manage a set of software timers.
thing TimerJava includes Timer
@pim "Timer"
@platform "java"
{
    property timer : JThread
    
    function start(delay : Integer) do
              /*if (not(timer == 'null')) do
                '' & timer & '.interrupt();'
             end*/
             'Thread t = new Thread(){
                public void run() {
				try {
					sleep(' & delay & ');'
					timer!timer_timeout()
				'} catch (InterruptedException ex) {
                    System.err.println("WARNING: " + ex.getMessage() + ". If you did not explicitly cancelled the timer, then it is a bug.");                   
				}  catch (Exception ex) {
                    ex.printStackTrace();
				} finally {
                    yield();
                    interrupt();
                }
				    
			    }
              };'
             timer = 't' 
              '' & timer & '.start();'                
    end
            
    function cancel() do
              if (not(timer == 'null')) do
                'try {'
                '' & timer & '.interrupt();'
                '} catch (SecurityException ex) {
                    System.err.println("WARNING: " + ex.getMessage());                   
				}  catch (Exception ex) {
                    ex.printStackTrace();
				}'
              end
    end

    statechart SoftTimer init default {
        state default {

          internal start
            event m : timer?timer_start
            guard m.delay > 0
            action start(m.delay)
            
          internal cancel
            event m : timer?timer_cancel
            action cancel()
        }
    }
}

configuration TestTimerJava @debug "true" {
    instance timer : TimerJava
    instance client : SimpleTimerClient
        set client.t1 = 500
    connector client.timer => timer.timer    
}
