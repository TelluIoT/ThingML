//IMPORTANT NOTE to run this example:
// 0) download and install https://github.com/warmcat/libwebsockets (follow their instructions)
//      NOTE : command 'cmake ..' must be run from the libwebsockets/build directory (build should be created manually)
//	NOTE : libssl-dev should be installed before
// 1) download and install from https://github.com/SINTEF-9012/TinyWebSockets (make installwebsockets && make && make install)
//      NOTE : both libwebsockets and TinyWebSockets should be in the same directory



//NOTE:
//make sure that the dynamic linker is set to look up /usr/local/lib
//for more details see http://lonesysadmin.net/2013/02/22/error-while-loading-shared-libraries-cannot-open-shared-object-file/

import "../websocket.thingml"
import "serializer.thingml"
import "timer.thingml"

object WebSocketClient
@c_type "WebSocketClient*";

thing WebSClient includes WebSocketServer, ArraySerializerCPP, ArrayDeserializerCPP
@c_header "
#include <stdio.h>
#include <string>
#include <iostream>
#include <stdlib.h>
#include <cstdarg>
#include \"tinyws/client-server/WebSocketFacade.h\"
#include \"tinyws/client-server/WebSocketClient.h\"

using namespace WebSockets;
"
{
    property client : WebSocketClient
    property _host : CString
    
    property status : WS_ERR
    
    provided port serialized @sync_send "true" {
    	sends receive_bytes
    	receives write_bytes
    }
        
    function on_open_callback_client()
		@c_prototype "void on_open_callback_client(void *_instance, ...)"
		@c_instance_var_name "(WebSClient_Instance *) _instance"
    do
    	'printf("\tWebSocketClient: on_open_callback\n");'
    	lifecycle!onOpen()
    end
    
    function on_close_callback_client()
		@c_prototype "void on_close_callback_client(void *_instance, ...)"
		@c_instance_var_name "(WebSClient_Instance *) _instance"
    do
    	'printf("\tWebSocketClient: on_close_callback\n");'
    	lifecycle!onClose()
    end
    
    function on_error_callback_client()
		@c_prototype "void on_error_callback_client(void *_instance, ...)"
		@c_instance_var_name "(WebSClient_Instance *) _instance"
    do
        'va_list arguments;'
        'va_start(arguments, _instance);'
        'char* message = va_arg(arguments, char*);'
    	'va_end(arguments);'
    	'printf("\tWebSocketClient: on_error_callback with message -> %s\n", message);'
    	err!onError('message')
    end
    
    function on_message_callback_client()
		@c_prototype "void on_message_callback_client(void *_instance, ...)"
		@c_instance_var_name "(WebSClient_Instance *) _instance"
    do
        'va_list arguments;'
        'va_start(arguments, _instance);'
        'char* message = va_arg(arguments, char*);'
    	'va_end(arguments);'
    	'printf("\tWebSocketClient: on_message_callback with message -> %s\n", message);'
    	var bytes : Byte[256]
    	'f_WebSClient_stringToBytes((WebSClient_Instance *) _instance, message, ' & bytes & ');'
    	//stringToBytes('message', bytes)
    	serialized!receive_bytes(bytes)
    end
    
    function init_client()
    do
    	 client = 'WebSocketFacade::InitWebSocketClient('& _host &', atoi('& _port &'), NULL);'
    	'ThingMLCallback* tml_op_open_callback = new ThingMLCallback(on_open_callback_client, _instance);'
		'ThingMLCallback* tml_op_close_callback = new ThingMLCallback(on_close_callback_client, _instance);'
		'ThingMLCallback* tml_op_error_callback = new ThingMLCallback(on_error_callback_client, _instance);'
		'ThingMLCallback* tml_op_message_callback = new ThingMLCallback(on_message_callback_client, _instance);'
		''& client &'->setCallbacks(tml_op_open_callback, tml_op_close_callback, tml_op_message_callback, tml_op_error_callback);'
    end
    
    function forward()
    do
    	'printf("forward is called...\n");'
		index = DATA_POSITION
		var code : Byte = buffer[CODE_POSITION]
		'printf("code is  %d...\n", ' & code & ');'
		if (code == 1) do
			var chars : Char[1024]
			var data : String = deserializeString(chars)
			'printf("data is received  \'%s\'...\n", ' & data & ');'
			messaging!onMessage(data)
		end
	end
    
    statechart behavior init Init {
    
        state Init {
            
            on entry do
                'printf("\tINIT CLIENT ON HOST: %s %s\n",'& _host &', '& _port& ');'
               	init_client()
            end
            
            transition -> Start
            event lifecycle?start_client
            
        }
        
        state Start {
            on entry do
            	'printf("\tclient attempts to establish connection\n");'
                 '' & client & '->open();'
                lifecycle!client_launched()
            end
            
            transition -> Serve
            event messaging?send
        }
        
        state Serve {
            on entry do
            	'printf("\tclient is in a serving state\n");'
            end
            
            internal event s : messaging?send
            action do
                ssetHeader(1, 5) //length of the data chunck + 1 byte to stor a string length
                serializeString("ping")
                send()
            end
            
            transition -> Stopped
            event lifecycle?stop_client
            
            internal event e : serialized?write_bytes
            action do
				var mess : Char[1024] = ""
				bytesToString(e.b, mess)
				'' & client & '->sendMessage('& mess &');'
            end
            
            internal event e: in?receive_bytes
            action do
            	print "receieved bytes on IN port\n"
            	receive(e.b)
            end
        }
        
        state Stopped {
            
            on entry '' & client & '->close();'
            
            transition -> Start
            event lifecycle?start_client
        }
                
    }  
}

thing Lifecicle includes WebSocketTest, ClockMsgs {
    required port signal {
        receives clock_tick
    }
    
    property pingmessage : String = "ping"
    
    statechart behavior init RunClient {

        internal event e : lifecycle?onOpen
        action print("test::onOpen")
        
        internal event lifecycle?onClose
        action print("test::onClose")
        
        internal event err?onError
        action print("test::onError")
        
        internal event m : messaging?onMessage
        action
        do
        	'printf("test::onMessage %s\n",' & m.msg &');'
        end
        
        
        state RunClient {
            on entry do
                print "\trunning client...\n"
                lifecycle!start_client()
            end
            
            transition -> Loop
            event e : lifecycle ? client_launched
        }
        
        state Loop {
            
            transition -> Loop
            event t : signal?clock_tick
            action do
                print "sending ping...\n"
                messaging!send(pingmessage)
            end
        }
    }
}

configuration WSClientSerialization
  @output_folder "/home/tmp"
  @debug "true"
  @debug_fifo "true"
  @debug_message_send ".*"
  @debug_message_receive ".*"
  @add_c_libraries "pthread websockets tinyws"
{

 
    instance client : WebSClient
       set client._port = "7681"
       set client._host = "localhost"
       
    connector client.out => client.serialized
    connector client.in =>client.serialized
       
    instance clock : ClockLinux
    	set clock.period = 5000    
    instance lifecicle : Lifecicle
     
    connector lifecicle.signal => clock.signal
    connector lifecicle.err => client.err
    connector lifecicle.lifecycle => client.lifecycle
    connector lifecicle.messaging => client.messaging
}
