import "../timer.thingml"

datatype ScalaTimer
@scala_type "org.thingml.utils.Timer";

// Manage a set of software timers.
thing TimerScala includes Timer
@pim "Timer"
@platform "scala"
@scala_trait "org.thingml.utils.TimerTask"
{

    property scalaTimer : ScalaTimer

    function onTimeout()@override "true" 
    do
        timer!timer_timeout()
    end
    
    function cancel()
    do
      if(not (scalaTimer == 'null'))
      do
        '' & scalaTimer & ' ! "cancel"'
      end
    end
    
    function start(delay : Integer) : Integer
    do
        scalaTimer = 'new org.thingml.utils.Timer(this,' & delay & ').start'
    end
    
    statechart SoftTimer init default {
    
        internal start
            event m : timer?timer_start
            guard m.delay > 0
            action start(m.delay)
            
        internal cancel
            event m : timer?timer_cancel
            action cancel()
    
        state default {}
        
    }
}