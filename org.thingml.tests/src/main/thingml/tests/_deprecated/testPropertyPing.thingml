import "../../../../../org.thingml.samples/src/main/thingml/thingml.thingml"

thing PingMsg {
    message ping(num : Int16);
    message pong(num : Int16);        
}

thing PingServer includes PingMsg {
    
    provided port pp {
         receives ping
         sends pong
    }
    
    statechart PingServer init Idle {
    
        state Idle {
            internal event p : pp?ping
            action pp!pong(p.num)    
        }    
    } 
}

thing TestPropertyPing includes Test, PingMsg 
@test "ppfpppff # P1P1P0P1P1P1P0P0"
@conf "instance pingServ : PingServer"
@conf "	set test.num = 1"
@conf "connector test.ppclient => pingServ.pp"
{
	property num : Int16
	
	required port ppclient {
        sends ping
        receives pong    
    }
    
    statechart TestPropertyPing init Idle {
    
    	state Idle {
    		transition -> Ping
    		event m : harnessIn?testIn
    		guard m.c == '\'p\''
    		after ppclient!ping(num)
    		transition -> Ping 
    		event m : harnessIn?testIn
    		guard m.c == '\'f\''
    		after ppclient!ping(0) // 0 != num.
    	}
    	
    	state Ping {
    		on entry harnessOut!testOut('\'P\'')
    		transition -> Idle
    		event p : ppclient?pong
    		guard p.num == num
    		after harnessOut!testOut ('\'1\'')
    		transition -> Idle
    		event p : ppclient?pong
    		guard not (p.num == num)
    		after harnessOut!testOut ('\'0\'')
    	}
    }
}