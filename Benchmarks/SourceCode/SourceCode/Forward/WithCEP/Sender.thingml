datatype Double<8> 
    @js_type "double";

thing fragment SenderMsgs {
    message forwardMessage();
    message forwardMessageWithParams(p1 : Double, p2 : Double, p3 : Double, p4 : Double, p5 : Double);
    message forwardWithModif(p1 : Double, p2 : Double, p3 : Double, p4 : Double, p5 : Double);
    
    message endBench();
    message redo();
}

thing Sender includes SenderMsgs {
    required port senderCepService {
        sends forwardMessage, forwardMessageWithParams, forwardWithModif, endBench
    }
    
    required port senderService {
        sends forwardMessage, forwardMessageWithParams, forwardWithModif, endBench
        receives redo
    }
        
    property nbIter : Double = 1
    property nbReco : Double = 100000000
    
    statechart Sender init Init { 
        state Init {
         on entry do
            var i :Double = 0
            while (i<nbIter) do
                senderCepService!forwardMessage()
                senderCepService!forwardMessageWithParams(1,2,3,4,5)
                senderCepService!forwardWithModif(1,2,3,4,5)
                i = i + 1
            end
            senderCepService!endBench()
            nbReco = nbReco - 1
            print "nbReco = " + nbReco
         end
         
         transition -> Idle  
        }
        
        state Idle {
            transition -> Init
            event senderService?redo
            guard nbReco > 0
            
            transition -> End
            event senderService?redo
            guard nbReco == 0
        }
        
        state End {
            on entry do
                print "Bye"   
            end 
        }
    }
}
