import "thingml.thingml"

thing fragment CepMsgs
{
  message m(v : Int16)
  message cep(v : Int16)
}

thing TestCepBufferAccess1 includes CepMsgs, Test
@c_header "#include <Arduino.h>"
@test " # O"
@conf "connector test.rcvPort => test.sendPort"
{

  provided port sendPort {
    sends m, cep
  }

  required port rcvPort {
    receives m, cep
  }

  function sum(values: Int16[], size: UInt8) : Int16 do
    var tmp: Int16 = 0
    var index : UInt8 = 0
    while (index < size) do
      tmp = tmp + values[index]
      index = index + 1
    end
    return tmp
  end


  stream aggregate
  from e @Buffer "4" @BufferSize "getMsgSize" : rcvPort?m::buffer 1 by 4
  select var sum : Int16 = sum(e.v[], 'getMsgSize')
  produce sendPort!cep(sum)

  statechart Sample init Init {
    state Init {
      on entry do
        sendPort!m(1)
        sendPort!m(2)
        sendPort!m(3)
        sendPort!m(4)
      end

      internal event e: rcvPort?cep
      action do
        if (e.v == 10) do
          harnessOut!testOut('\'O\'')
        end
      end
    }
  }
}
